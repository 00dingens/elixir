% Holds introspection for methods.
% To check how methods are defined internally, check elixir_def_method.
-module(elixir_methods).
-export([assert_behavior/2, inherit_methods/1, mixin_methods/1]).
-include("elixir.hrl").
-import(lists, [umerge/2, sort/1]).

% Public in Elixir

mixin_methods(#elixir_slate__{module=Module}) ->
  convert_export(Module);

mixin_methods(#elixir_module__{data=Data} = Self) when is_atom(Data) ->
  calculate_methods(Self, fun owner_methods/1, elixir_module_behavior:mixins(Self), []);

mixin_methods(#elixir_module__{name=Module}) ->
  convert_export(Module);

mixin_methods(Self) ->
  Mixin = elixir_dispatch:builtin_mixin(Self),
  convert_export(Mixin).

% Public in Erlang

assert_behavior(Module, Object) when is_atom(Module) -> 
  assert_behavior(Module:module_info(exports) -- elixir_internal(), Object);

assert_behavior(Exports, Object) -> 
  Methods = mixin_methods(Object),
  lists:foreach(fun({Name, Arity}) ->
    case lists:member({Name, Arity-1}, Methods) of
      true -> [];
      false -> elixir_errors:error({nocallback, {Name, Arity-1, Object}})
    end
  end, Exports).

inherit_methods(Name) ->
  owner_methods(Name) -- elixir_compiled().

%% HELPERS

convert_export(Module) ->
  convert_methods(Module:module_info(exports) -- elixir_internal()).

% Returns all methods in a module skipping inherited ones.
owner_methods(Name) when is_atom(Name) ->
  Attributes = elixir_constants:lookup(Name, attributes),
  Public = case proplists:get_value(public, Attributes) of
    undefined -> [];
    Else -> Else
  end,
  convert_methods(Public).

% Keeps a list of all methods automatically generated by Elixir, with Elixir arity.
elixir_compiled() ->
  [{'__mixins__',0},{'__module_name__',0},{'__module__',0}].

% Keeps a list of all methods that are generated automatically but should be hidden in Elixir.
elixir_internal() ->
  [{module_info,0},{module_info,1},{'__elixir_exported__',2}].

% Convert methods from Elixir arity to Erlang.
convert_methods(Target) ->
  lists:map(fun convert_method/1, Target).

convert_method({Name, Arity}) -> { Name, Arity - 1 }.

% Merge methods from mixins ensuring uniqueness.
calculate_methods(_Self, Fun, List, Acc) ->
  calculate_methods(Fun, List, Acc).

calculate_methods(Fun, [H|T], Acc) ->
  calculate_methods(Fun, T, umerge(Acc, sort(Fun(H))));

calculate_methods(Fun, [], Acc) ->
  Acc.